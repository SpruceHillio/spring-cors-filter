buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.sprucehill'
archivesBaseName = 'spring-cors-filter'
version = '1.0.0'
description = 'A CORS filter configurable implementation for your Spring application'

processResources {
    expand(project.properties)
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework:spring-core:4.2.1.RELEASE'
    compile 'org.springframework:spring-web:4.2.1.RELEASE'
    compile 'org.slf4j:slf4j-api:1.7.12'
    provided 'javax.servlet:javax.servlet-api:3.0.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Spring CORS Filter'
                packaging 'jar'
                description 'A CORS filter configurable implementation for your Spring application'
                url 'https://github.com/SpruceHillio/spring-cors-filter'

                scm {
                    connection 'scm:git:git://github.com/SpruceHillio/spring-cors-filter.git'
                    developerConnection 'scm:git:git://github.com/SpruceHillio/spring-cors-filter.git'
                    url 'https://github.com/SpruceHillio/spring-cors-filter.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'duergner'
                        name 'Michael Duergner'
                        email 'michael@sprucehill.io'
                        organization 'SpruceHill.io GmbH'
                    }
                }
            }
        }
    }
}
